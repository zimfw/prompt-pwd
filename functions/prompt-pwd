# vim:et sts=2 sw=2 ft=zsh
setopt LOCAL_OPTIONS EXTENDED_GLOB

local git_root current_dir separator
local -i tail_len dir_len
zstyle -s ':zim:prompt-pwd:tail' length 'tail_len' || tail_len=0
if zstyle -t ':zim:prompt-pwd' git-root; then
  git_root=${PWD}
  while true; do
    if [[ -e ${git_root}/.git ]]; then
      current_dir=${PWD#${git_root:h}/}
      break
    fi
    if [[ ${git_root} == / ]]; then
      current_dir=${(%):-%${tail_len}~}
      break
    fi
    git_root=${git_root:h}
  done
else
  current_dir=${(%):-%${tail_len}~}
fi
zstyle -s ':zim:prompt-pwd:separator' format 'separator' || separator=/
zstyle -s ':zim:prompt-pwd:fish-style' dir-length 'dir_len' || dir_len=0
if [[ ${dir_len} -gt 0 || ${separator} != / ]]; then
  local current_dirs=("${(@s:/:)current_dir}")
  if (( dir_len > 0 && ${#current_dirs} > 2 )); then
    current_dirs[2,-2]=("${(@M)current_dirs[2,-2]##.#?(#c,${dir_len})}")
  fi
  separator=${(e)separator}
  current_dir="${(@pj:$separator:)current_dirs}"
fi
print -Rn ${current_dir}
