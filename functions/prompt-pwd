# vim:et sts=2 sw=2 ft=zsh
setopt LOCAL_OPTIONS EXTENDED_GLOB

local git_root current_dir separator path_format path_length
local -i dir_length
zstyle -s ':zim:prompt-pwd:path-format' format 'path_format' || path_format=""
zstyle -s ':zim:prompt-pwd:path-length' format 'path_length' || path_length=0
if zstyle -t ':zim:prompt-pwd' git-root && \
    git_root=$(command git rev-parse --show-toplevel 2>/dev/null); then
  current_dir="${$(pwd -P)#${git_root:h}/}"
elif [[ -n $path_format ]]; then
  current_dir="$path_format"
elif [[ $path_length -gt 0 ]]; then
  current_dir=%($((path_length+2))~|%-1~/.../%$path_length~|%~)
else
  current_dir=${(%):-%~}
fi
zstyle -s ':zim:prompt-pwd:separator' format 'separator' || separator='/'
zstyle -s ':zim:prompt-pwd:fish-style' dir-length 'dir_length' || dir_length=0
if [[ ${dir_length} -gt 0 || ${separator} != '/' ]]; then
  local current_dirs=("${(@s:/:)current_dir}")
  if (( dir_length > 0 && ${#current_dirs} > 2 )); then
    current_dirs[2,-2]=("${(@M)current_dirs[2,-2]##.#?(#c,${dir_length})}")
  fi
  separator=${(e)separator}
  current_dir="${(@pj:$separator:)current_dirs}"
fi
print -Rn ${current_dir}
