# vim:et sts=2 sw=2 ft=zsh
builtin emulate -L zsh -o EXTENDED_GLOB

local __current_dir __separator __git_root_truncate __git_root_format __git_root
local -i __tail_len __dir_len __git_root_offset
zstyle -s ':zim:prompt-pwd:tail' length '__tail_len' || __tail_len=0
zstyle -b ':zim:prompt-pwd' git-root '__git_root_truncate'
zstyle -s ':zim:prompt-pwd:git-root' format '__git_root_format'
if [[ ${__git_root_truncate} == yes || -n ${__git_root_format} ]]; then
  __git_root=${PWD}
  __git_root_offset=-1
  while true; do
    if [[ -e ${__git_root}/.git ]]; then
      if [[ ${__git_root_truncate} == yes ]]; then
        __current_dir=${PWD#${__git_root:h}/}
      else
        __current_dir=${(%):-%${__tail_len}~}
      fi
      if [[ -z ${__git_root_format} || abs(${__git_root_offset}) -gt ${__tail_len} ]]; then
        __git_root_offset=0
      fi
      break
    fi
    if [[ ${__git_root} == / ]]; then
      __current_dir=${(%):-%${__tail_len}~}
      __git_root_offset=0
      break
    fi
    __git_root=${__git_root:h}
    (( __git_root_offset-- ))
  done
else
  __current_dir=${(%):-%${__tail_len}~}
  __git_root_offset=0
fi
zstyle -s ':zim:prompt-pwd:separator' format '__separator' || __separator=/
zstyle -s ':zim:prompt-pwd:fish-style' dir-length '__dir_len' || __dir_len=0
if [[ ${__dir_len} -gt 0 || ${__separator} != / || ${__git_root_offset} -lt 0 ]]; then
  local __current_dirs=("${(@s:/:)__current_dir}")
  if (( __dir_len > 0 && ${#__current_dirs} > 2 )); then
    __current_dirs[2,-2]=("${(@M)__current_dirs[2,-2]##.#?(#c,${__dir_len})}")
  fi
  if (( __git_root_offset < 0 )); then
    zformat -f __git_root ${(e)__git_root_format} d:${__current_dirs[${__git_root_offset}]}
    __current_dirs[${__git_root_offset}]=${__git_root}
  fi
  __separator=${(e)__separator}
  __current_dir="${(@pj:$__separator:)__current_dirs}"
fi
if (( # )); then
  typeset -g "${^@}=${__current_dir}"
else
  print -Rn ${__current_dir}
fi
